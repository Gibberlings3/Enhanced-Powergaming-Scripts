COPY ~enhanced-powergaming-scripts/baf/upload/mocore.baf~ ~enhanced-powergaming-scripts/baf/upload/mocore.baf~

///////////////////////////////////////////////////////////////////////////
// Add Selunes Blessing and Aura of Protection                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// 5e Spellcasting Prebuff - DSotSC Selunes Blessing and Aura of Protection~~~~~
    string = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpellRES(CLERIC_SELUNES_BLESSING)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  !HasImmunityEffects(Myself)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES(CLERIC_SELUNES_BLESSING,Myself)
    SetGlobalTimer("MO_SpellCast","LOCALS",3)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpellRES(CLERIC_AURA_OF_PROTECTION)
  !CheckSpellState(Myself,AURA_OF_PROT)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES(CLERIC_AURA_OF_PROTECTION,Myself)
    SetGlobalTimer("MO_SpellCast","LOCALS",3)
END
~~~~~ //"
  END
