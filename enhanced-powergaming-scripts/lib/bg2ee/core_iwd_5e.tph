COPY ~enhanced-powergaming-scripts/baf/upload/mocore.baf~ ~enhanced-powergaming-scripts/baf/upload/mocore.baf~

///////////////////////////////////////////////////////////////////////////
// Add Mind Blank                                                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// 5e Spellcasting - IWD Mind Blank and Impervious Sanctity of Mind~~~~~
    string = ~~~~~IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES(SPWI801)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetInterrupt(FALSE)
    SpellRES(SPWI801,Myself)
    SetInterrupt(TRUE)
END

IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES(SPPR736)
  !CheckSpellState(Myself,CHAOTIC_COMMANDS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SpellRES(SPPR736,Myself)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Add Divine Protection and Greater Divine Protection                   //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// 5e Spellcasting - IWD (Greater) Divine Protection~~~~~
    string = ~~~~~IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HaveSpellRES(SPPR738)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SpellRES(SPPR738,Myself)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HaveSpellRES(SPPR525)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SpellRES(SPPR525,Myself)
END
~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Update Cure Moderate Wounds                                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// 5e Spellcasting - IWD Cure Moderate Wounds~~~~~
    string = ~~~~~IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_CastingHeal","GLOBAL",1)
  ActionListEmpty()
  OR(2)
    !See([EVILCUTOFF])
    CheckStat(Myself,1,SANCTUARY)
  HaveSpellRES(SPPR218)
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),80)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  !CheckSpellState(LastSeenBy(Myself),MINOR_GLOBE)
  !CheckSpellState(LastSeenBy(Myself),GLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetGlobalTimer("MO_HealCast","GLOBAL",12)
    SetGlobal("MO_CastingHeal","GLOBAL",1)
    SetInterrupt(FALSE)
    SpellRES(SPPR218,LastSeenBy(Myself))
    SetInterrupt(TRUE)
END
~~~~~ //"
  END