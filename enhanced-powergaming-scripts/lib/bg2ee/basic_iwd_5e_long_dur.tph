COPY ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~ ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~

///////////////////////////////////////////////////////////////////////////
// Add Mind Blank and Impervious Sanctity of Mind                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// 5e Spellcasting - IWD Mind Blank and Impervious Sanctity of Mind~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !See([EVILCUTOFF])
  Global("MO_WildMindBlank","LOCALS",0)
  HaveSpellRES(WIZARD_MIND_BLANK)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_WildMindBlank","LOCALS",1)
    SetGlobalTimer("MO_SpellCast","LOCALS",2)
END

IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !See([EVILCUTOFF])
  Global("MO_WildMindBlank","LOCALS",1)
  HaveSpellRES(WIZARD_MIND_BLANK)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetGlobal("MO_WildMindBlank","LOCALS",0)
    SetInterrupt(FALSE)
    SpellRES(WIZARD_MIND_BLANK,Myself)
    SetInterrupt(TRUE)
END

IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES(CLERIC_IMPERVIOUS_SANCTITY_OF_MIND)
  !CheckSpellState(Myself,CHAOTIC_COMMANDS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SpellRES(CLERIC_IMPERVIOUS_SANCTITY_OF_MIND,Myself)
END
~~~~~ //"
  END