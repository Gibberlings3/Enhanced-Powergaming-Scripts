COPY ~enhanced-powergaming-scripts/baf/upload/mocore.baf~ ~enhanced-powergaming-scripts/baf/upload/mocore.baf~

///////////////////////////////////////////////////////////////////////////
// Add Mind Blank                                                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    warn = 0
    STR_VAR
    pattern = ~~~~~// NOT HAS Mind Blank Check~~~~~
    string = ~~~~~!CheckSpellState(LastSeenBy(Myself),MIND_BLANK)~~~~~ //"
  END

  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// IWD Mind Blank and Impervious Sanctity of Mind~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !See([EVILCUTOFF])
  Global("MO_WildMindBlank","LOCALS",0)
  HaveSpell(WIZARD_MIND_BLANK)  // spwi806.SPL (Mind Blank)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_WildMindBlank","LOCALS",1)
    SetGlobalTimer("MO_SpellCast","LOCALS",2)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !See([EVILCUTOFF])
  Global("MO_WildMindBlank","LOCALS",1)
  HaveSpell(WIZARD_MIND_BLANK)  // spwi806.SPL (Mind Blank)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetGlobal("MO_WildMindBlank","LOCALS",0)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_MIND_BLANK)  // spwi806.SPL (Mind Blank)
    SetInterrupt(TRUE)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_IMPERVIOUS_SANCTITY_OF_MIND)  // sppr735.SPL (Impervious Sanctity of Mind)
  !CheckSpellState(Myself,CHAOTIC_COMMANDS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    Spell(Myself,CLERIC_IMPERVIOUS_SANCTITY_OF_MIND)  // sppr735.SPL (Impervious Sanctity of Mind)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Add Divine Protection and Greater Divine Protection                   //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// IWD (Greater) Divine Protection~~~~~
    string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HaveSpell(CLERIC_GREATER_SHIELD_OF_LATHANDER)  // sppr737.SPL (Greater Divine Protection)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    Spell(Myself,CLERIC_GREATER_SHIELD_OF_LATHANDER)  // sppr737.SPL (Greater Divine Protection)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HaveSpell(CLERIC_SHIELD_OF_LATHANDER)  // sppr520.SPL (Divine Protection)
  !CheckSpellState(Myself,SHIELD_OF_LATHANDER)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    Spell(Myself,CLERIC_SHIELD_OF_LATHANDER)  // sppr520.SPL (Divine Protection)
END
~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Update Cure Moderate Wounds                                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// IWD Cure Moderate Wounds~~~~~
    string = ~~~~~IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_CastingHeal","GLOBAL",1)
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  OR(2)
    !See([EVILCUTOFF])
    CheckStat(Myself,1,SANCTUARY)
  HaveSpell(CLERIC_CURE_MODERATE_WOUNDS)  // sppr215.SPL (Cure Moderate Wounds)
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),80)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  !CheckSpellState(LastSeenBy(Myself),MINOR_GLOBE)
  !CheckSpellState(LastSeenBy(Myself),GLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetGlobalTimer("MO_HealCast","GLOBAL",12)
    SetGlobal("MO_CastingHeal","GLOBAL",1)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_MODERATE_WOUNDS)  // sppr215.SPL (Cure Moderate Wounds)
    SetInterrupt(TRUE)
END
~~~~~ //"
  END