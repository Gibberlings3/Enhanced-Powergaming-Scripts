COPY ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~ ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~

  //==========================================================================
  LPF REPLACE_MULTILINE
    INT_VAR
    num = 1
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END~~~~~
    string = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END

IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
  !CheckSpellState(LastSeenBy(Myself),GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(CLERIC_IRONSKIN)  // SPPR506.SPL (Stoneskin)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_IRONSKIN)  // SPPR506.SPL (Stoneskin)
END

IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","LOCALS",2)
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("Boot12",Myself)  // Gargoyle Boots
    IncrementGlobal("MO_UsedGargoyle","LOCALS",1)
END

IF
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","LOCALS",2)
  HasItem("Boot12",Myself)  // Gargoyle Boots
  HasItemEquiped("Boot01",Myself)  // Boots of Speed
THEN
  RESPONSE #100
    XEquipItem("Boot12",Myself,SLOT_BOOTS,EQUIP)  // Gargoyle Boots
END

IF
  OR(2)
    CheckStatGT(Myself,0,STONESKINS)
    GlobalGT("MO_UsedGargoyle","LOCALS",1)
  HasItem("Boot01",Myself)  // Boots of Speed
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    XEquipItem("Boot01",Myself,SLOT_BOOTS,EQUIP)  // Boots of Speed
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !See([EVILCUTOFF])
  HaveSpell(WIZARD_ARMOR)  // SPWI102.SPL (Mage Armor)
  !CheckSpellState(Myself,MAGE_ARMOR)
  !CheckSpellState(LastSeenBy(Myself),MINOR_GLOBE)
  !CheckSpellState(LastSeenBy(Myself),GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_ARMOR)  // SPWI102.SPL (Mage Armor)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !See([EVILCUTOFF])
  HaveSpell(WIZARD_MIND_BLANK)  // SPWI802.SPL (Mind Blank)
  !CheckSpellState(Myself,MIND_BLANK)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_MIND_BLANK)  // SPWI802.SPL (Mind Blank)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  Global("MO_TURNUNDEAD","LOCALS",1)
  !CheckStat(Myself,1,SANCTUARY)
  Class(Myself,CLERIC_ALL)
  !ModalState(TURNUNDEAD)
THEN
  RESPONSE #100
    Turn()
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !See([EVILCUTOFF])
  !ModalState(DETECTTRAPS)
  !ModalState(STEALTH)
  OR(3)
    Class(Myself,THIEF_ALL)
    Class(Myself,MONK)
    Class(Myself,SHAMAN)
THEN
  RESPONSE #100
    FindTraps()
END

IF
  Class(Myself,BARD_ALL)
  !ModalState(BATTLESONG)
THEN
  RESPONSE #100
    BattleSong()
END