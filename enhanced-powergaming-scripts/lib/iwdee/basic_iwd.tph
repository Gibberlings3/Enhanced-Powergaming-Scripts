COPY ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~ ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~

///////////////////////////////////////////////////////////////////////////
// Remove BGII: EE Item Variables                                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  PartyRested()
THEN
  RESPONSE #100
    SetGlobal("MO_UsedIlbratha","GLOBAL",0)
    SetGlobal("MO_UsedDuplication","GLOBAL",0)
    SetGlobal("MO_UsedGargoyle","GLOBAL",0)
    SetGlobal("MO_UsedCheetah","GLOBAL",0)
    SetGlobal("MO_UsedDevout","GLOBAL",0)
    SetGlobal("MO_UsedFortitude","GLOBAL",0)
    SetGlobal("MO_UsedSpellTurning","GLOBAL",0)
    SetGlobal("MO_UsedClkOfStars","GLOBAL",0)
    SetGlobal("MO_UsedCowlOfStars","GLOBAL",0)
    SetGlobal("MO_UsedBltMnrInv","GLOBAL",0)
    SetGlobal("MO_UsedClkDrkMN","GLOBAL",0)
    SetGlobal("MO_UsedShldFyrus","GLOBAL",0)
END

IF
  Global("MO_TrueSightActive","LOCALS",1)
  CheckStat(Myself,0,TRUE_SIGHT)
THEN
  RESPONSE #100
    SetGlobal("MO_TrueSightActive","LOCALS",0)
    SetGlobal("MO_PartyTrueSight","GLOBAL",0)
END~~~~~
    string = ~~~~~IF
  Global("MO_TrueSightActive","LOCALS",1)
  CheckStat(Myself,0,TRUE_SIGHT)
THEN
  RESPONSE #100
    SetGlobal("MO_TrueSightActive","LOCALS",0)
    SetGlobal("MO_PartyTrueSight","GLOBAL",0)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Cowled Wizards Check                                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  !HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)  // SPPR408.SPL (Protection From Evil, 10' Radius)
  Global("MO_ProtEvil","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_ProtEvil","LOCALS",0)
    SetGlobal("MO_TeamProtEvil","GLOBAL",0)
END

IF
  OR(7)
    AreaCheck("AR0700")  // Waukeen's Promenade
    AreaCheck("AR0020")  // City Gates
    AreaCheck("AR0800")  // Graveyard
    AreaCheck("AR0400")  // Slums
    AreaCheck("AR0300")  // The Docks
    AreaCheck("AR1000")  // Government District
    AreaCheck("AR0900")  // Temple District
  Global("BRIBEDCOWLED","GLOBAL",0)
  GlobalLT("CowledWarning","GLOBAL",7)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",0)
END

IF
  !AreaCheck("AR0700")  // Waukeen's Promenade
  !AreaCheck("AR0020")  // City Gates
  !AreaCheck("AR0800")  // Graveyard
  !AreaCheck("AR0400")  // Slums
  !AreaCheck("AR0300")  // The Docks
  !AreaCheck("AR1000")  // Government District
  !AreaCheck("AR0900")  // Temple District
  Global("MO_CastAthkatla","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",1)
END

IF
  OR(2)
    Global("BRIBEDCOWLED","GLOBAL",1)
    Global("CowledWarning","GLOBAL",7)
  Global("MO_CastAthkatla","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",1)
END~~~~~
    string = ~~~~~IF
  !HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)  // SPPR408.SPL (Protection From Evil, 10' Radius)
  Global("MO_ProtEvil","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_ProtEvil","LOCALS",0)
    SetGlobal("MO_TeamProtEvil","GLOBAL",0)
END~~~~~ //"
  END

  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)~~~~~
    string = ~~~~~!CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Replace Modal Dialogue (Pre-Buffing/Turn Undead)                      //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_TURNUNDEAD","LOCALS",0)
  Class(Myself,CLERIC_ALL)
  HotKey(V)
THEN
  RESPONSE #100
    SetGlobal("MO_TURNUNDEAD","LOCALS",1)
    DisplayStringHead(Myself,50702)  // Turning Undead
END

IF
  Global("MO_TURNUNDEAD","LOCALS",1)
  HotKey(V)
THEN
  RESPONSE #100
    SetGlobal("MO_TURNUNDEAD","LOCALS",0)
    DisplayStringHead(Myself,50703)  // Stopped Turning Undead
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(D)
THEN
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91061)  // Right. I'll get started then.
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(D)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END~~~~~
    string = ~~~~~IF
  Global("MO_TURNUNDEAD","LOCALS",0)
  Class(Myself,CLERIC_ALL)
  HotKey(V)
THEN
  RESPONSE #100
    SetGlobal("MO_TURNUNDEAD","LOCALS",1)
    DisplayStringHead(Myself,24499)  // Turning Undead constantly if idle
END

IF
  Global("MO_TURNUNDEAD","LOCALS",1)
  HotKey(V)
THEN
  RESPONSE #100
    SetGlobal("MO_TURNUNDEAD","LOCALS",0)
    DisplayStringHead(Myself,24500)  // Stopped Turning Undead
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(D)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,48286)  // I'll start out at once.
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(D)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,11521)  // As ready as it could be.
END~~~~~ //"
  END

  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END~~~~~
    string = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,11521)  // As ready as it could be.
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Cloak of the Dark Moon                                         //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("OHRCLCK4",Myself)  // Cloak of the Dark Moon
  GlobalLT("MO_UsedClkDrkMN","GLOBAL",3)
  !CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    UseItem("OHRCLCK4",Myself)  // Cloak of the Dark Moon
    IncrementGlobal("MO_UsedClkDrkMN","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Shield of Fyrus Khal                                           //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("OHNSHLD1",Myself)  // Shield of Fyrus Khal +2
  GlobalLT("MO_UsedShldFyrus","GLOBAL",1)
  !CheckSpellState(Myself,SPELL_TURNING)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    UseItem("OHNSHLD1",Myself)  // Shield of Fyrus Khal +2
    IncrementGlobal("MO_UsedShldFyrus","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Ring of Spell Turning                                          //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("RING34",Myself)  // Ring of Spell Turning
  GlobalLT("MO_UsedSpellTurning","GLOBAL",1)
  !CheckSpellState(Myself,SPELL_TURNING)
THEN
  RESPONSE #100
    UseItem("RING34",Myself)  // Ring of Spell Turning
    IncrementGlobal("MO_UsedSpellTurning","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Amulet of Cheetah Speed                                        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  !CheckStatGT(Myself,0,IMPROVEDHASTE)
  GlobalLT("MO_UsedCheetah","GLOBAL",1)
  HasItemEquiped("AMUL26",Myself)  // Amulet of Cheetah Speed
THEN
  RESPONSE #100
    UseItem("AMUL26",Myself)  // Amulet of Cheetah Speed
    IncrementGlobal("MO_UsedCheetah","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Ilbratha+1 and Ring of Duplication                             //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedIlbratha","GLOBAL",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
THEN
  RESPONSE #100
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIlbratha","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","GLOBAL",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
THEN
  RESPONSE #100
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Belt of Minor Invulnerability                                  //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  GlobalLT("MO_UsedBltMnrInv","GLOBAL",1)
  HasItemEquiped("OHRBELT2",Myself)  // Belt of Minor Invulnerability
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    UseItem("OHRBELT2",Myself)  // Belt of Minor Invulnerability
    IncrementGlobal("MO_UsedBltMnrInv","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Hardiness                                                      //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
  !CheckSpellState(Myself,HARDINESS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Physical Mirror                                                //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpell(CLERIC_PHYSICAL_MIRROR)  // sppr525.spl (Physical Mirror)
  CheckStatLT(Myself,1,CLERIC_PHYSICAL_MIRROR)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_PHYSICAL_MIRROR)  // sppr525.spl (Physical Mirror)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Righteous Magic                                                //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpell(CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Magic)
  !CheckSpellState(Myself,RIGHTEOUS_MAGIC)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Magic)
END~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Update Negative Plane Protection                                      //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
  !HasItemEquiped("AMUL21",Myself)  // Amulet of Power
  !HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +2
  !HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5~~~~~
    string = ~~~~~CheckStat(LastSeenBy(Myself),0,LEVEL_DRAIN_IMMUNITY)~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Remove Gargoyle Boots, Girdle of Fortitude, and Cloak of Stars        //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","GLOBAL",2)
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
  !CheckSpellState(Myself,GLOBE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("Boot12",Myself)  // Gargoyle Boots
    IncrementGlobal("MO_UsedGargoyle","GLOBAL",1)
END

IF
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","GLOBAL",2)
  HasItem("Boot12",Myself)  // Gargoyle Boots
  HasItemEquiped("Boot01",Myself)  // The Paws of the Cheetah
THEN
  RESPONSE #100
    XEquipItem("Boot12",Myself,SLOT_BOOTS,EQUIP)  // Gargoyle Boots
END

IF
  OR(2)
    CheckStatGT(Myself,0,STONESKINS)
    GlobalGT("MO_UsedGargoyle","GLOBAL",1)
  HasItem("Boot01",Myself)  // The Paws of the Cheetah
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    XEquipItem("Boot01",Myself,SLOT_BOOTS,EQUIP)  // The Paws of the Cheetah
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedFortitude","GLOBAL",1)
  HasItem("BELT09",Myself)  // Girdle of Fortitude
  CheckStatLT(Myself,18,CON)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("BELT09",Myself)  // Girdle of Fortitude
    IncrementGlobal("MO_UsedFortitude","GLOBAL",1)
END

IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedClkOfStars","GLOBAL",1)
  HasItemEquiped("CLCK25",Myself)  // Cloak of the Stars
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("CLCK25",Myself)  // Cloak of the Stars
    IncrementGlobal("MO_UsedClkOfStars","GLOBAL",1)
END

IF~~~~~
    string = ~~~~~IF~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Update Local Timers                                                   //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~ONE_ROUND~~~~~
    string = ~~~~~6~~~~~ //"
  END