COPY ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~ ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~

///////////////////////////////////////////////////////////////////////////
// Gargoyle Boots, Belt of Fortitude, and Cloak of Stars                 //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// bg2ee long duration item 1~~~~~
      string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","GLOBAL",2)
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    UseItem("Boot12",Myself)  // Gargoyle Boots
    IncrementGlobal("MO_UsedGargoyle","GLOBAL",1)
END

IF
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","GLOBAL",2)
  HasItem("Boot12",Myself)  // Gargoyle Boots
  HasItemEquiped("Boot01",Myself)  // The Paws of the Cheetah
THEN
  RESPONSE #100
    XEquipItem("Boot12",Myself,SLOT_BOOTS,EQUIP)  // Gargoyle Boots
END

IF
  OR(2)
    CheckStatGT(Myself,0,STONESKINS)
    GlobalGT("MO_UsedGargoyle","GLOBAL",1)
  HasItem("Boot01",Myself)  // The Paws of the Cheetah
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    XEquipItem("Boot01",Myself,SLOT_BOOTS,EQUIP)  // The Paws of the Cheetah
END

IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedFortitude","GLOBAL",1)
  HasItem("BELT09",Myself)  // Girdle of Fortitude
  CheckStatLT(Myself,18,CON)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    UseItem("BELT09",Myself)  // Girdle of Fortitude
    IncrementGlobal("MO_UsedFortitude","GLOBAL",1)
END

IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedClkOfStars","GLOBAL",1)
  HasItemEquiped("CLCK25",Myself)  // Cloak of the Stars
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    UseItem("CLCK25",Myself)  // Cloak of the Stars
    IncrementGlobal("MO_UsedClkOfStars","GLOBAL",1)
END~~~~~ //"
  END

///////////////////////////////////////////////////////////////////////////
// Cowl of the Stars                                                     //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// bg2ee long duration item 2~~~~~
      string = ~~~~~IF
  OR(2)
    ActionListEmpty()
    !CheckSpellState(Myself,CASTING_SPELL)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedCowlOfStars","GLOBAL",3)
  HasItemEquiped("OHRCLCK1",Myself)  // Cowl of the Stars
  !HasItem("melfmet",Myself)  // Melf's Minute Meteor
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",6)
    SetGlobal("MO_HasMelfMeteors","LOCALS",1)
    UseItem("OHRCLCK1",Myself)  // Cowl of the Stars
    IncrementGlobal("MO_UsedCowlOfStars","GLOBAL",1)
END~~~~~ //"
  END
