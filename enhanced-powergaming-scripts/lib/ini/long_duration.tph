COPY ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~ ~enhanced-powergaming-scripts/baf/upload/mobasic.baf~

///////////////////////////////////////////////////////////////////////////
// Update Basic AI to Attack                                             //
///////////////////////////////////////////////////////////////////////////
  LPF REPLACE_MULTILINE
    INT_VAR
    strict = 1
    verbose = 0
    STR_VAR
    pattern = ~~~~~// ini long duration~~~~~
      string = ~~~~~// 5e Spellcasting Stoneskin

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  See([EVILCUTOFF])
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
    SetInterrupt(TRUE)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !See([EVILCUTOFF])
  !EEex_LuaTrigger("return AmMoving()")
  Global("MO_WildStoneskin","LOCALS",0)
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_WildStoneskin","LOCALS",1)
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",2)
END

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  !See([EVILCUTOFF])
  !EEex_LuaTrigger("return AmMoving()")
  Global("MO_WildStoneskin","LOCALS",1)
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    SetGlobal("MO_WildStoneskin","LOCALS",0)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
    SetInterrupt(TRUE)
END

// 5e Spellcasting Iron Skins

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !EEex_LuaTrigger("return AmMoving()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  HaveSpell(CLERIC_IRONSKIN)  // SPPR506.SPL (Iron Skins)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_IRONSKIN)  // SPPR506.SPL (Iron Skins)
    SetInterrupt(TRUE)
END

// bg2ee long duration item 1

// 5e Spellcasting - IWD Mind Blank and Impervious Sanctity of Mind

// IWD Mind Blank and Impervious Sanctity of Mind

IF
  !HasItem("melfmet",Myself)  // Melf's Minute Meteor
  Global("MO_HasMelfMeteors","LOCALS",1)
  CanEquipRanged()
THEN
  RESPONSE #100
    SetGlobal("MO_HasMelfMeteors","LOCALS",0)
    EquipRanged()
    ActionOverride(Myself,ClearActions(Myself))
END

IF
  !HasItem("melfmet",Myself)  // Melf's Minute Meteor
  Global("MO_HasMelfMeteors","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_HasMelfMeteors","LOCALS",0)
    EquipMostDamagingMelee()
    ActionOverride(Myself,ClearActions(Myself))
END

// 5e Spellcasting Melf's Minute Meteors

IF
  OR(2)
    ActionListEmpty()
    EEex_LuaTrigger("return AmAttacking()")
  !EEex_LuaTrigger("return AmMoving()")
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  HaveSpell(WIZARD_MELF_METEOR)  // SPWI325.SPL (Melf's Minute Meteors)
  !HasItem("melfmet",Myself)  // Melf's Minute Meteor
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobal("MO_SpellCast","LOCALS",1)
    SetGlobal("MO_HasMelfMeteors","LOCALS",1)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_MELF_METEOR)  // SPWI325.SPL (Melf's Minute Meteors)
    SetInterrupt(TRUE)
END

// bg2ee long duration item 2
~~~~~ //"
  END
