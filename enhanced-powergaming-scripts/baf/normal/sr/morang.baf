// **********************************************************************
// *           Segment Name: morang.baf
// **********************************************************************
// *
// * morang.baf
// * -----------
// * AI Script for:             BGEET/BG2EE with Spell Revisions
// * Written by:                Mark Evinger (morpheus562)
// * Version:                   Version 3.0 (22 May 2021)
// * Comments to:               mark.evinger@hotmail.com
// * Depends on:                BGEET/BG2EE
//                              Spell Revisions (Revised)
//
//
// * Description:
// *
// * This is a script specifically designed for the Ranger class and kits. 
// * This script will cast pre-buff spells when the D key is pressed. The following 
// * spells will be pre-buffed: Armor of Faith, Divine Might, and Protection 
// * from Fire. Will pre-buff using the following potions: Potion of Heroism, 
// * Potion of Fire/Cold Resistance, Potion of Absorption, Potion of Magic 
// * Protection, and Potion of Invulnerability. Finally, the Ranger will pre-buff 
// * using mirror image from either offhand Ilbrath* or the Ring of Duplication.
//
// * In addition to pre-buffing, the script will intelligently heal and use 
// * abilities both in and out of combat. Items used by the script are: 
// * Ilbratha +1, Ring of Duplication, Amulet of the Cheetah, and Gargoyle Boots.

IF
	PartyRested()
THEN
	RESPONSE #100
		SetGlobal("MO_UsedIbratha","LOCALS",0)
		SetGlobal("MO_UsedDuplication","LOCALS",0)
		SetGlobal("MO_UsedGargoyle","LOCALS",0)
		SetGlobal("MO_UsedCheetah","LOCALS",0)
END

IF
	Global("MO_BUFF","LOCALS",0)
	HotKey(D)
THEN
	RESPONSE #50
		SetGlobal("MO_BUFF","LOCALS",1)
		DisplayStringHead(Myself,91061)  // Right. I'll get started then.
	RESPONSE #50
		SetGlobal("MO_BUFF","LOCALS",1)
		DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
	Global("MO_BUFF","LOCALS",1)
	OR(2)
		HotKey(D)
		See([EVILCUTOFF])
THEN
	RESPONSE #100
		SetGlobal("MO_BUFF","LOCALS",0)
		DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn09",Myself)  // Potion of Heroism
	!CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
	RESPONSE #100
		UseItem("potn09",Myself)  // Potion of Heroism
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
	RESPONSE #100
		UseItem("potn02",Myself)  // Potion of Fire Resistance
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
	RESPONSE #100
		UseItem("potn22",Myself)  // Potion of Cold Resistance
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn18",Myself)  // Potion of Absorption
	CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
	RESPONSE #100
		UseItem("potn18",Myself)  // Potion of Absorption
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn34",Myself)  // Potion of Magic Blocking
THEN
	RESPONSE #100
		UseItem("potn34",Myself)  // Potion of Magic Blocking
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
	CheckStatGT(Myself,0,ARMORCLASS)
THEN
	RESPONSE #100
		UseItem("potn11",Myself)  // Potion of Invulnerability
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Resist Fear)
	OR(6)
		!CheckSpellState(Myself,RESIST_FEAR)
		!CheckSpellState(NearestAllyOf(Myself),RESIST_FEAR)
		!CheckSpellState(SecondNearestAllyOf(Myself),RESIST_FEAR)
		!CheckSpellState(ThirdNearestAllyOf(Myself),RESIST_FEAR)
		!CheckSpellState(FourthNearestAllyOf(Myself),RESIST_FEAR)
		!CheckSpellState(FifthNearestAllyOf(Myself),RESIST_FEAR)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Resist Fear)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!CheckStatGT(Myself,0,IMPROVEDHASTE)
	GlobalLT("MO_UsedCheetah","LOCALS",1)
	HasItemEquiped("AMUL26",Myself)  // Amulet of Cheetah Speed
THEN
	RESPONSE #100
		UseItem("AMUL26",Myself)  // Amulet of Cheetah Speed
		IncrementGlobal("MO_UsedCheetah","LOCALS",1)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_PROTECTION_FROM_FIRE)  // sppr521.spl (Protection from Fire)
	CheckStatLT(Myself,80,RESISTFIRE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_PROTECTION_FROM_FIRE)  // sppr521.spl (Protection from Fire)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPRA303")
  !CheckSpellState(Myself,PROTECTION_FROM_NORMAL_MISSILES)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES("SPRA303",Myself)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpellRES("SPIN103")  // Draw Upon Divine Might
	!StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
	CheckStatLT(Myself,24,DEX)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		SpellRES("SPIN103",Myself)  // Draw Upon Divine Might
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	GlobalLT("MO_UsedIbratha","LOCALS",1)
	HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
	RESPONSE #100
		UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
		IncrementGlobal("MO_UsedIbratha","LOCALS",1)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	GlobalLT("MO_UsedDuplication","LOCALS",3)
	HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
THEN
	RESPONSE #100
		UseItem("OHRRING1",Myself)  // Ring of Duplication
		IncrementGlobal("MO_UsedDuplication","LOCALS",1)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
	!CheckSpellState(Myself,HARDINESS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
	Global("MO_BUFF","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("MO_BUFF","LOCALS",0)
		DisplayStringHead(Myself,421)  // Combat ready!
END
