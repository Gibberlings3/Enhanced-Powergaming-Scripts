// **********************************************************************
// *           Segment Name: mocore.baf
// **********************************************************************
// *
// * mocore.baf
// * -----------
// * AI Script for:             BG2EE & BGEET
// * Written by:                Mark Evinger (morpheus562)
// * Version:                   Version 3.0 (22 May 2021)
// * Comments to:               mark.evinger@hotmail.com
// * Depends on:                BG2EE/BGEET
// *							Spell Revisions
//
//
// * Description:
// * This script is designed for all core classes: Fighter, Mage, Thief, Cleric, Druid, and 
// * any combination of multiclass/dualclass to include kits. This script will constantly 
// * Turn Undead when the V key is pressed and will cast pre-buffing spells when the D key 
// * is pressed. If the character is able, the script will constantly be detecting traps.

IF
	PartyRested()
THEN
	RESPONSE #100
		SetGlobal("MO_UsedIlbratha","GLOBAL",0)
		SetGlobal("MO_UsedDuplication","GLOBAL",0)
		SetGlobal("MO_UsedGargoyle","GLOBAL",0)
		SetGlobal("MO_UsedCheetah","GLOBAL",0)
END

IF
	HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)
	OR(2)
		Global("MO_ProtEvil","LOCALS",0)
		Global("MO_TeamProtEvil","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("MO_ProtEvil","LOCALS",1)
		SetGlobal("MO_TeamProtEvil","GLOBAL",1)
END

IF
	!HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)
	Global("MO_ProtEvil","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("MO_ProtEvil","LOCALS",0)
		SetGlobal("MO_TeamProtEvil","GLOBAL",0)
END

IF
	OR(7)
		AreaCheck("AR0700")  // Waukeen's Promenade
		AreaCheck("AR0020")  // City Gates
		AreaCheck("AR0800")  // Graveyard
		AreaCheck("AR0400")  // Slums
		AreaCheck("AR0300")  // The Docks
		AreaCheck("AR1000")  // Government District
		AreaCheck("AR0900")  // Temple District
	Global("BRIBEDCOWLED","GLOBAL",0)
	GlobalLT("CowledWarning","GLOBAL",7)
	Global("MO_CastAthkatla","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("MO_CastAthkatla","LOCALS",0)
END

IF
	!AreaCheck("AR0700")  // Waukeen's Promenade
	!AreaCheck("AR0020")  // City Gates
	!AreaCheck("AR0800")  // Graveyard
	!AreaCheck("AR0400")  // Slums
	!AreaCheck("AR0300")  // The Docks
	!AreaCheck("AR1000")  // Government District
	!AreaCheck("AR0900")  // Temple District
	Global("MO_CastAthkatla","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("MO_CastAthkatla","LOCALS",1)
END

IF
	OR(2)
		Global("BRIBEDCOWLED","GLOBAL",1)
		Global("CowledWarning","GLOBAL",7)
	Global("MO_CastAthkatla","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("MO_CastAthkatla","LOCALS",1)
END

IF
	Global("MO_TURNUNDEAD","LOCALS",0)
	Class(Myself,CLERIC_ALL)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("MO_TURNUNDEAD","LOCALS",1)
		DisplayStringHead(Myself,50702)  // Turning Undead
END

IF
	Global("MO_TURNUNDEAD","LOCALS",1)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("MO_TURNUNDEAD","LOCALS",0)
		DisplayStringHead(Myself,50703)  // Stopped Turning Undead
END

IF
	Global("MO_BUFF","LOCALS",0)
	HotKey(D)
THEN
	RESPONSE #50
		SetGlobal("MO_BUFF","LOCALS",1)
		DisplayStringHead(Myself,91061)  // Right. I'll get started then.
	RESPONSE #50
		SetGlobal("MO_BUFF","LOCALS",1)
		DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
	Global("MO_BUFF","LOCALS",1)
	OR(2)
		HotKey(D)
		See([EVILCUTOFF])
THEN
	RESPONSE #100
		SetGlobal("MO_BUFF","LOCALS",0)
		DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn09",Myself)  // Potion of Heroism
	!CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
	RESPONSE #100
		UseItem("potn09",Myself)  // Potion of Heroism
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
	RESPONSE #100
		UseItem("potn02",Myself)  // Potion of Fire Resistance
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
	RESPONSE #100
		UseItem("potn22",Myself)  // Potion of Cold Resistance
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn18",Myself)  // Potion of Absorption
	CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
	RESPONSE #100
		UseItem("potn18",Myself)  // Potion of Absorption
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn34",Myself)  // Potion of Magic Blocking
THEN
	RESPONSE #100
		UseItem("potn34",Myself)  // Potion of Magic Blocking
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn07",Myself)  // Potion of Storm Giant Strength
	CheckStatLT(Myself,24,STR)
THEN
	RESPONSE #100
		UseItem("potn07",Myself)  // Potion of Storm Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn06",Myself)  // Potion of Cloud Giant Strength
	CheckStatLT(Myself,23,STR)
THEN
	RESPONSE #100
		UseItem("potn06",Myself)  // Potion of Cloud Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn05",Myself)  // Potion of Fire Giant Strength
	CheckStatLT(Myself,22,STR)
THEN
	RESPONSE #100
		UseItem("potn05",Myself)  // Potion of Fire Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn04",Myself)  // Potion of Frost Giant Strength
	CheckStatLT(Myself,21,STR)
THEN
	RESPONSE #100
		UseItem("potn04",Myself)  // Potion of Frost Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn12",Myself)  // Potion of Stone Giant Strength
	CheckStatLT(Myself,20,STR)
THEN
	RESPONSE #100
		UseItem("potn12",Myself)  // Potion of Stone Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn03",Myself)  // Potion of Hill Giant Strength
	CheckStatLT(Myself,19,STR)
THEN
	RESPONSE #100
		UseItem("potn03",Myself)  // Potion of Hill Giant Strength
END

IF
	Global("MO_BUFF","LOCALS",1)
	HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
	CheckStatGT(Myself,0,ARMORCLASS)
THEN
	RESPONSE #100
		UseItem("potn11",Myself)  // Potion of Invulnerability
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	GlobalLT("MO_UsedIlbratha","GLOBAL",1)
	HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
	RESPONSE #100
		UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
		IncrementGlobal("MO_UsedIlbratha","GLOBAL",1)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	GlobalLT("MO_UsedDuplication","GLOBAL",3)
	HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
THEN
	RESPONSE #100
		UseItem("OHRRING1",Myself)  // Ring of Duplication
		IncrementGlobal("MO_UsedDuplication","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_AID)  // SPPR201.SPL (Aid)
  !StateCheck(Myself,STATE_AID)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,CLERIC_AID)  // SPPR201.SPL (Aid)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_ENERGY)  // SPWI606.SPL (Protection from Magic Energy)
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)  // SPWI606.SPL (Protection from Magic Energy)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_PROTECTION_FROM_FIRE)  // sppr521.spl (Protection from Fire)
	CheckStatLT(Myself,80,RESISTFIRE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_PROTECTION_FROM_FIRE)  // sppr521.spl (Protection from Fire)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(Myself)
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See([PC])
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(SecondNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(ThirdNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(FourthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(FifthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
	See(SixthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),DEATH_WARD)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_DEATH_WARD)  // SPPR409.SPL (Death Ward)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(Myself)
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See([PC])
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(SecondNearest([PC]))
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(ThirdNearest([PC]))
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(FourthNearest([PC]))
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(FifthNearest([PC]))
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	See(SixthNearest([PC]))
	!CheckSpellState(LastSeenBy(Myself),BARBARIAN_RAGE)
	!CheckSpellState(LastSeenBy(Myself),BERSERKER_RAGE)
	!CheckSpellState(LastSeenBy(Myself),CHAOTIC_COMMANDS)
	HaveSpell(CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_CHAOTIC_COMMANDS)  // SPPR508.SPL (Chaotic Commands)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(Myself)
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See([PC])
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(SecondNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(ThirdNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(FourthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(FifthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
	See(SixthNearest([PC]))
	!Kit(LastSeenBy(Myself),UNDEADHUNTER)
	!CheckSpellState(LastSeenBy(Myself),LATHANDER_BOON)
	!CheckSpellState(LastSeenBy(Myself),LEVEL_DRAIN_IMMUNITY)
	!HasItemEquipedReal("BLUN25",LastSeenBy(Myself))  // Mace of Disruption +4
	!HasItemEquipedReal("SW1H69",LastSeenBy(Myself))  // Spectral Brand +5
	!StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	!CheckSpellState(LastSeenBy(Myself),MINOR_SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),SPELL_DEFLECTION)
	!CheckSpellState(LastSeenBy(Myself),GREATER_SPELL_DEFLECTION)
	!CheckStat(LastSeenBy(Myself),1,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(LastSeenBy(Myself),CLERIC_NEGATIVE_PLANE_PROTECTION)  // SPPR413.SPL (Negative Plane Protection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_GHOST_ARMOR)  // SPWI317.SPL (Ghost Armor)
	CheckStatGT(Myself,2,ARMORCLASS)
	!CheckSpellState(LastSeenBy(Myself),GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_GHOST_ARMOR)  // SPWI317.SPL (Ghost Armor)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(WIZARD_SHIELD)  // SPWI114.SPL (Shield)
  !CheckSpellState(Myself,WIZARD_SHIELD)
  !CheckSpellState(LastSeenBy(Myself),GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_SHIELD)  // SPWI114.SPL (Shield)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(WIZARD_BLUR)  // SPWI201.SPL (Blur)
  !StateCheck(Myself,STATE_BLUR)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_BLUR)  // SPWI201.SPL (Blur)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_RESIST_FEAR)  // SPWI210.SPL (Resist Fear)
	!CheckSpellState(Myself,RESIST_FEAR)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_RESIST_FEAR)  // SPWI210.SPL (Resist Fear)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_REMOVE_FEAR)
	!CheckSpellState(Myself,RESIST_FEAR)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_REMOVE_FEAR)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_PROTECTION_FROM_EVIL_10_FOOT)  // SPPR408.SPL (Magic Circle Against Evil)
	CheckStat(Myself,0,PROTECTION_FROM_EVIL)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_PROTECTION_FROM_EVIL_10_FOOT)  // SPPR408.SPL (Magic Circle Against Evil)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
	!CheckSpellState(Myself,ARMOR_OF_FAITH)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_BLESS)  // SPPR101.SPL (Bless)
	!StateCheck(Myself,STATE_BLESS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_BLESS)  // SPPR101.SPL (Bless)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_LUCK)  // SPWI209.SPL (Luck)
	!StateCheck(Myself,STATE_LUCK)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_LUCK)  // SPWI209.SPL (Luck)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_PROTECTION_FROM_THE_ELEMENTS)  // SPWI702.SPL (Protection from the Elements)
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_THE_ELEMENTS)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_PROTECTION_FROM_THE_ELEMENTS)  // SPWI702.SPL (Protection from the Elements)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_PROTECTION_FROM_NORMAL_MISSILES)  // SPWI311.SPL (Protection from Missiles)
	!CheckSpellState(Myself,PROTECTION_FROM_NORMAL_MISSILES)
	!CheckSpellState(Myself,MINOR_GLOBE)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)  // SPWI311.SPL (Protection from Missiles)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_PHYSICAL_MIRROR)  // SPPR613.SPL (Physical Mirror)
	CheckStatLT(Myself,1,CLERIC_PHYSICAL_MIRROR)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_PHYSICAL_MIRROR)  // SPPR613.SPL (Physical Mirror)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_SPELL_SHIELD)  // SPWI519.SPL (Spell Shield)
	!CheckSpellState(Myself,SPELL_SHIELD)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_SHIELD)  // SPWI519.SPL (Spell Shield)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_GREATER_SPELL_DEFLECTION)  // SPWI701.SPL (Greater Spell Deflection)
	!CheckSpellState(Myself,GREATER_SPELL_DEFLECTION)
	!CheckSpellState(Myself,SPELL_DEFLECTION)
	!CheckSpellState(Myself,MINOR_SPELL_DEFLECTION)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_GREATER_SPELL_DEFLECTION)  // SPWI701.SPL (Greater Spell Deflection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_SPELL_DEFLECTION)  // SPWI522.SPL (Spell Deflection)
	!CheckSpellState(Myself,GREATER_SPELL_DEFLECTION)
	!CheckSpellState(Myself,SPELL_DEFLECTION)
	!CheckSpellState(Myself,MINOR_SPELL_DEFLECTION)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_DEFLECTION)  // SPWI522.SPL (Spell Deflection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_MINOR_SPELL_DEFLECTION)  // SPWI318.SPL (Minor Spell Deflection)
	!CheckSpellState(Myself,GREATER_SPELL_DEFLECTION)
	!CheckSpellState(Myself,SPELL_DEFLECTION)
	!CheckSpellState(Myself,MINOR_SPELL_DEFLECTION)
	!CheckSpellState(Myself,MINOR_GLOBE)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)  // SPWI318.SPL (Minor Spell Deflection)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	HaveSpell(WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
	!CheckSpellState(Myself,MINOR_GLOBE)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckStat(Myself,1,PUPPETTYPE)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_DISPELLING_SCREEN)  // spwi525.spl (Dispelling Screen)
	!CheckSpellState(Myself,DISPEL_SCREEN)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_DISPELLING_SCREEN)  // spwi525.spl (Dispelling Screen)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpellRES("SPIN103")  // Draw Upon Divine Might
	!StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
	!CheckSpellState(Myself,MINOR_GLOBE)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,25,STR)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		SpellRES("SPIN103",Myself)  // Draw Upon Divine Might
END

IF
	Global("MO_BUFF","LOCALS",1)
	HaveSpell(CLERIC_CHANT)  // SPPR203.SPL (Chant)
	!StateCheck(Myself,STATE_CHANT)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_CHANT)  // SPPR203.SPL (Chant)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)  // SPPR214.SPL (Draw Upon Divine Might)
	!StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
	CheckStatLT(Myself,25,STR)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)  // SPPR214.SPL (Draw Upon Divine Might)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpellRES("SPIN103")  // Draw Upon Divine Might
	!StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
	CheckStatLT(Myself,25,STR)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
THEN
	RESPONSE #100
		SpellRES("SPIN103",Myself)  // Draw Upon Divine Might
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_DEFENSIVE_HARMONY)  // SPPR406.SPL (Defensive Harmony)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_DEFENSIVE_HARMONY)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_DEFENSIVE_HARMONY)  // SPPR406.SPL (Defensive Harmony)
END

IF
	Global("MO_BUFF","LOCALS",1)
	HaveSpell(CLERIC_SHIELD_OF_THE_ARCHONS)  // SPPR701.SPL (Shield of the Archons)
	CheckStatLT(Myself,1,CLERIC_SHIELD_OF_THE_ARCHONS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_SHIELD_OF_THE_ARCHONS)  // SPPR701.SPL (Shield of the Archons)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_AURA_OF_FLAMING_DEATH)  // SPPR730.SPL (Aura of Flaming Death)
	CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
	CheckStatLT(Myself,1,CLERIC_BLADE_BARRIER)
	CheckStatLT(Myself,89,RESISTFIRE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_AURA_OF_FLAMING_DEATH)  // SPPR730.SPL (Aura of Flaming Death)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_GLOBE_OF_BLADES)  // SPPR725.SPL (Globe of Blades)
	CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
	CheckStatLT(Myself,1,CLERIC_BLADE_BARRIER)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_GLOBE_OF_BLADES)  // SPPR725.SPL (Globe of Blades)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(CLERIC_BLADE_BARRIER)  // SPPR603.SPL (Blade Barrier)
	CheckStat(Myself,0,WIZARD_FIRE_SHIELD)
	CheckStatLT(Myself,1,CLERIC_BLADE_BARRIER)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_BLADE_BARRIER)  // SPPR603.SPL (Blade Barrier)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(BERSERKER_RAGE)  // SPCL321.SPL (Enrage)
	!CheckSpellState(Myself,BERSERKER_RAGE_FATIGUE)
	!CheckSpellState(Myself,BERSERKER_RAGE)
THEN
	RESPONSE #100
		Spell(Myself,BERSERKER_RAGE)  // SPCL321.SPL (Enrage)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_PROTECTION_FROM_ENERGY)  // SPWI803.SPL (Protection from Energy)
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_ENERGY)
	!CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_THE_ELEMENTS)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_PROTECTION_FROM_ENERGY)  // SPWI803.SPL (Protection from Energy)
END

IF
	Global("MO_BUFF","LOCALS",1)
	HaveSpell(CLERIC_MAGIC_RESISTANCE)  // SPPR509.SPL (Magic Resistance)
	CheckStatLT(Myself,20,RESISTMAGIC)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_MAGIC_RESISTANCE)  // SPPR509.SPL (Magic Resistance)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_MESTILS_ACID_SHEATH)  // SPWI524.spl (Mestil's Acid Sheath)
	!CheckSpellState(Myself,RED_FIRESHIELD)
	!CheckSpellState(Myself,ACID_SHEATH)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_MESTILS_ACID_SHEATH)  // SPWI524.spl (Mestil's Acid Sheath)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield)
	!CheckSpellState(Myself,RED_FIRESHIELD)
	!CheckSpellState(Myself,ACID_SHEATH)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_FIRE_SHIELD_RED)  // SPWI418.SPL (Fire Shield)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_HASTE)  // SPWI305.SPL (Haste)
	!StateCheck(Myself,STATE_HASTED)
	CheckStat(Myself,0,IMPROVEDHASTE)
	!StateCheck(Myself,STATE_NOT_TARGETABLE)
	!CheckSpellState(Myself,MINOR_GLOBE)
	!CheckSpellState(Myself,GLOBE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_HASTE)  // SPWI305.SPL (Haste)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_IMPROVED_HASTE)  // SPWI613.SPL (Improved Haste)
	!CheckStatGT(Myself,0,IMPROVEDHASTE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_IMPROVED_HASTE)  // SPWI613.SPL (Improved Haste)
END

IF
	Global("MO_BUFF","LOCALS",1)
	HaveSpell(CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Fury)
	!CheckSpellState(Myself,RIGHTEOUS_MAGIC)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Fury)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WIZARD_SPELL_TRAP)  // SPWI902.SPL (Spell Trap)
	CheckStatLT(Myself,1,WIZARD_SPELL_TRAP)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	Global("MO_CastAthkatla","LOCALS",1)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WIZARD_SPELL_TRAP)  // SPWI902.SPL (Spell Trap)
END

IF
	Global("MO_BUFF","LOCALS",1)
	IfValidForPartyDialog(Myself)
	HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
	!CheckSpellState(Myself,HARDINESS)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	!CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
	!StateCheck(Myself,STATE_SILENCED)
	!CheckSpellState(Myself,WILD_MAGIC_AREA)
	!CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
	RESPONSE #100
		Spell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
	Global("MO_BUFF","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("MO_BUFF","LOCALS",0)
		DisplayStringHead(Myself,421)  // Combat ready!
END