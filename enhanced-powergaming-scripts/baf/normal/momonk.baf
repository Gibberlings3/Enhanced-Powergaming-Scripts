// **********************************************************************
// *           Segment Name: momonk.baf
// **********************************************************************
// *
// * momonk.baf
// * -----------
// * AI Script for:             BG2EE & BGEET
// * Written by:                Mark Evinger (morpheus562)
// * Version:                   Version 3.0 (18 June 2021)
// * Comments to:               mark.evinger@hotmail.com
// * Depends on:                BG2EE
//
//
// * Description:
// * This is a script specifically designed for the Monk class. The script will constantly 
// * be detecting traps while idle. This script will cast pre-buff spells when the D key is 
// * pressed. The following spells will be pre-buffed: Blur, Mirror Image, Hardiness, Will 
// * pre-buff using the following potions: Potion of Heroism, Potion of Fire/Cold Resistance, 
// * Potion of Absorption, Potion of Magic Protection, Potion of Invulnerability, and all of 
// * the strength potions. Finally, this character will pre-buff using mirror image from either 
// * offhand Ilbratha or the Ring of Duplication, Righteous Magic, and Improved Haste from the 
// * Amulet of Cheetah Speed. In addition to pre-buffing, the script will intelligently heal 
// * and use abilities both in and out of combat. Items used by the script are: Ilbratha +1, 
// * Ring of Duplication, Amulet of the Cheetah, Headband of the Devout, and Gargoyle Boots.

IF
  PartyRested()
THEN
  RESPONSE #100
    SetGlobal("MO_UsedIlbratha","GLOBAL",0)
    SetGlobal("MO_UsedDuplication","GLOBAL",0)
    SetGlobal("MO_UsedGargoyle","GLOBAL",0)
    SetGlobal("MO_UsedCheetah","GLOBAL",0)
    SetGlobal("MO_UsedFortitude","GLOBAL",0)
    SetGlobal("MO_UsedSpellTurning","GLOBAL",0)
    SetGlobal("MO_UsedClkOfStars","GLOBAL",0)
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(D)
THEN
  RESPONSE #50
    SetGlobal("MO_#BUFF","LOCALS",1)
    DisplayStringHead(Myself,91061)  // Right. I'll get started then.
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(D)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn09",Myself)  // Potion of Heroism
  !CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
  RESPONSE #100
    UseItem("potn09",Myself)  // Potion of Heroism
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
  RESPONSE #100
    UseItem("potn02",Myself)  // Potion of Fire Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
  RESPONSE #100
    UseItem("potn22",Myself)  // Potion of Cold Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn18",Myself)  // Potion of Absorption
  CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
  RESPONSE #100
    UseItem("potn18",Myself)  // Potion of Absorption
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn34",Myself)  // Potion of Magic Protection
THEN
  RESPONSE #100
    UseItem("potn34",Myself)  // Potion of Magic Protection
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn07",Myself)  // Potion of Storm Giant Strength
  CheckStatLT(Myself,24,STR)
THEN
  RESPONSE #100
    UseItem("potn07",Myself)  // Potion of Storm Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn06",Myself)  // Potion of Cloud Giant Strength
  CheckStatLT(Myself,23,STR)
THEN
  RESPONSE #100
    UseItem("potn06",Myself)  // Potion of Cloud Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn05",Myself)  // Potion of Fire Giant Strength
  CheckStatLT(Myself,22,STR)
THEN
  RESPONSE #100
    UseItem("potn05",Myself)  // Potion of Fire Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn04",Myself)  // Potion of Frost Giant Strength
  CheckStatLT(Myself,21,STR)
THEN
  RESPONSE #100
    UseItem("potn04",Myself)  // Potion of Frost Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn12",Myself)  // Potion of Stone Giant Strength
  CheckStatLT(Myself,20,STR)
THEN
  RESPONSE #100
    UseItem("potn12",Myself)  // Potion of Stone Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn03",Myself)  // Potion of Hill Giant Strength
  CheckStatLT(Myself,19,STR)
THEN
  RESPONSE #100
    UseItem("potn03",Myself)  // Potion of Hill Giant Strength
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
  CheckStatGT(Myself,0,ARMORCLASS)
THEN
  RESPONSE #100
    UseItem("potn11",Myself)  // Potion of Invulnerability
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HasItemEquiped("RING34",Myself)  // Ring of Spell Turning
  GlobalLT("MO_UsedSpellTurning","GLOBAL",1)
  !CheckSpellState(Myself,SPELL_TURNING)
THEN
  RESPONSE #100
    UseItem("RING34",Myself)  // Ring of Spell Turning
    IncrementGlobal("MO_UsedSpellTurning","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_BLUR)
  HaveSpellRES("SPDM102")  // Blur
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES("SPDM102",Myself)  // Blur
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  HaveSpellRES("SPDM106")  // Mirror Image
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES("SPDM106",Myself)  // Mirror Image
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","GLOBAL",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
THEN
  RESPONSE #100
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !Global("MO_UsedIlbratha","GLOBAL",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
  RESPONSE #100
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIlbratha","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !Global("MO_UsedIlbratha","GLOBAL",1)
  HasItem("SW1H26",Myself)  // Ilbratha +1
  HasItemEquiped("WANINJA",Myself)  // Ninjatō of the Scarlet Brotherhood +3
THEN
  RESPONSE #100
    XEquipItem("SW1H26",Myself,SLOT_SHIELD,EQUIP)  // Ilbratha +1
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  OR(2)
    StateCheck(Myself,STATE_MIRRORIMAGE)
    Global("MO_UsedIlbratha","GLOBAL",1)
  HasItem("WANINJA",Myself)  // Ninjatō of the Scarlet Brotherhood +3
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
  RESPONSE #100
    XEquipItem("WANINJA",Myself,SLOT_SHIELD,EQUIP)  // Ninjatō of the Scarlet Brotherhood +3
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  GlobalLT("MO_UsedDevout","GLOBAL",1)
  HasItemEquiped("OHRHELM1",Myself)  // Headband of the Devout
THEN
  RESPONSE #100
    UseItem("OHRHELM1",Myself)  // Headband of the Devout
    IncrementGlobal("MO_UsedDevout","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
  !CheckSpellState(Myself,HARDINESS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPCL237")  // Greater Sun
  CheckStatLT(Myself,50,RESISTFIRE)
  CheckStatLT(Myself,50,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SpellRES("SPCL237",Myself)  // Greater Sun
END

IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END