// **********************************************************************
// *           Segment Name: momonk.baf
// **********************************************************************
// *
// * Description:
// * This is a script specifically designed for the Monk class. The script will constantly 
// * be detecting traps while idle. This script will cast pre-buff spells when the D key is 
// * pressed. The following spells will be pre-buffed: Blur, Mirror Image, Hardiness, Will 
// * pre-buff using the following potions: Potion of Heroism, Potion of Fire/Cold Resistance, 
// * Potion of Absorption, Potion of Magic Protection, Potion of Invulnerability, and all of 
// * the strength potions. Finally, this character will pre-buff using mirror image from either 
// * offhand Ilbratha or the Ring of Duplication, and Righteous Magic. In addition to pre-buffing,
// * the script will intelligently heal and use abilities both in and out of combat. Items used 
// * by the script are: Ilbratha +1, Ring of Duplication, Amulet of the Cheetah, Headband of 
// * the Devout, and Gargoyle Boots.

IF
  !GlobalTimerNotExpired("MO_HealCast","GLOBAL")
  Global("MO_CastingHeal","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("MO_CastingHeal","GLOBAL",0)
END

IF
  PartyRested()
THEN
  RESPONSE #100
    SetGlobal("MO_UsedIlbratha","GLOBAL",0)
    SetGlobal("MO_UsedDuplication","GLOBAL",0)
    SetGlobal("MO_UsedGargoyle","GLOBAL",0)
    SetGlobal("MO_UsedCheetah","GLOBAL",0)
    SetGlobal("MO_UsedDevout","GLOBAL",0)
    SetGlobal("MO_UsedFortitude","GLOBAL",0)
    SetGlobal("MO_UsedSpellTurning","GLOBAL",0)
    SetGlobal("MO_UsedClkOfStars","GLOBAL",0)
    SetGlobal("MO_UsedCowlOfStars","GLOBAL",0)
    SetGlobal("MO_UsedBltMnrInv","GLOBAL",0)
    SetGlobal("MO_UsedClkDrkMN","GLOBAL",0)
    SetGlobal("MO_UsedShldFyrus","GLOBAL",0)
END

IF
  Global("MO_DISABLECOWLED","LOCALS",0)
  HotKey(hot_key_CowledCheck)
THEN
  RESPONSE #100
    SetGlobal("MO_DISABLECOWLED","LOCALS",1)
    SetGlobal("MO_CastAthkatla","LOCALS",1)
    DisplayStringHead(Myself,@5020)
END

IF
  Global("MO_DISABLECOWLED","LOCALS",1)
  HotKey(hot_key_CowledCheck)
THEN
  RESPONSE #100
    SetGlobal("MO_DISABLECOWLED","LOCALS",0)
    SetGlobal("MO_CastAthkatla","LOCALS",0)
    DisplayStringHead(Myself,@5021)
END

IF
  OR(8)
    AreaCheck("AR0700")  // Waukeen's Promenade
    AreaCheck("AR0020")  // City Gates
    AreaCheck("AR0800")  // Graveyard
    AreaCheck("AR0400")  // Slums
    AreaCheck("AR0300")  // The Docks
    AreaCheck("AR0500")  // Bridge District
    AreaCheck("AR1000")  // Government District
    AreaCheck("AR0900")  // Temple District
  Global("BRIBEDCOWLED","GLOBAL",0)
  Global("MO_DISABLECOWLED","LOCALS",0)
  GlobalLT("CowledWarning","GLOBAL",7)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",0)
END

IF
  !AreaCheck("AR0700")  // Waukeen's Promenade
  !AreaCheck("AR0020")  // City Gates
  !AreaCheck("AR0800")  // Graveyard
  !AreaCheck("AR0400")  // Slums
  !AreaCheck("AR0300")  // The Docks
  !AreaCheck("AR0500")  // Bridge District
  !AreaCheck("AR1000")  // Government District
  !AreaCheck("AR0900")  // Temple District
  Global("MO_CastAthkatla","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",1)
END

IF
  OR(2)
    Global("BRIBEDCOWLED","GLOBAL",1)
    Global("CowledWarning","GLOBAL",7)
  Global("MO_CastAthkatla","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MO_CastAthkatla","LOCALS",1)
END

IF
  Global("MO_DETECTILLUSIONS","LOCALS",0)
  OR(3)
    Class(Myself,THIEF_ALL)
    Class(Myself,MONK)
    Class(Myself,SHAMAN)
  HotKey(hot_key_DetectIllusion)
THEN
  RESPONSE #100
    SetGlobal("MO_DETECTILLUSIONS","LOCALS",1)
    DisplayStringHead(Myself,@5018)
END

IF
  Global("MO_DETECTILLUSIONS","LOCALS",1)
  HotKey(hot_key_DetectIllusion)
THEN
  RESPONSE #100
    SetGlobal("MO_DETECTILLUSIONS","LOCALS",0)
    DisplayStringHead(Myself,@5019)
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(hot_key_Buffing)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,@5002)
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(hot_key_Buffing)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,@5003)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn09",Myself)  // Potion of Heroism
  !CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
  RESPONSE #100
    UseItem("potn09",Myself)  // Potion of Heroism
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
  RESPONSE #100
    UseItem("potn02",Myself)  // Potion of Fire Resistance
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
  RESPONSE #100
    UseItem("potn22",Myself)  // Potion of Cold Resistance
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn18",Myself)  // Potion of Absorption
  CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
  RESPONSE #100
    UseItem("potn18",Myself)  // Potion of Absorption
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn34",Myself)  // Potion of Magic Protection
THEN
  RESPONSE #100
    UseItem("potn34",Myself)  // Potion of Magic Protection
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn07",Myself)  // Potion of Storm Giant Strength
  CheckStatLT(Myself,24,STR)
THEN
  RESPONSE #100
    UseItem("potn07",Myself)  // Potion of Storm Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn06",Myself)  // Potion of Cloud Giant Strength
  CheckStatLT(Myself,23,STR)
THEN
  RESPONSE #100
    UseItem("potn06",Myself)  // Potion of Cloud Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn05",Myself)  // Potion of Fire Giant Strength
  CheckStatLT(Myself,22,STR)
THEN
  RESPONSE #100
    UseItem("potn05",Myself)  // Potion of Fire Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn04",Myself)  // Potion of Frost Giant Strength
  CheckStatLT(Myself,21,STR)
THEN
  RESPONSE #100
    UseItem("potn04",Myself)  // Potion of Frost Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn12",Myself)  // Potion of Stone Giant Strength
  CheckStatLT(Myself,20,STR)
THEN
  RESPONSE #100
    UseItem("potn12",Myself)  // Potion of Stone Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  ActionListEmpty()
  HasItemEquiped("potn03",Myself)  // Potion of Hill Giant Strength
  CheckStatLT(Myself,19,STR)
THEN
  RESPONSE #100
    UseItem("potn03",Myself)  // Potion of Hill Giant Strength
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
  CheckStatGT(Myself,0,ARMORCLASS)
THEN
  RESPONSE #100
    UseItem("potn11",Myself)  // Potion of Invulnerability
    DisplayStringHead(Myself,@5005)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("OHRCLCK4",Myself)  // Cloak of the Dark Moon
  GlobalLT("MO_UsedClkDrkMN","GLOBAL",3)
  !CheckStatGT(Myself,0,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    UseItem("OHRCLCK4",Myself)  // Cloak of the Dark Moon
    IncrementGlobal("MO_UsedClkDrkMN","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("RING34",Myself)  // Ring of Spell Turning
  GlobalLT("MO_UsedSpellTurning","GLOBAL",1)
  !CheckSpellState(Myself,SPELL_TURNING)
THEN
  RESPONSE #100
    UseItem("RING34",Myself)  // Ring of Spell Turning
    IncrementGlobal("MO_UsedSpellTurning","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_BLUR)
  HaveSpellRES("SPDM102")  // Blur
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPDM102",Myself)  // Blur
    RemoveSpellRES("SPDM102")  // Blur
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  HaveSpellRES("SPDM106")  // Mirror Image
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPDM106",Myself)  // Mirror Image
    RemoveSpellRES("SPDM106")  // Mirror Image
END

IF
  Global("MO_BUFF","LOCALS",1)
  GlobalLT("MO_UsedBltMnrInv","GLOBAL",1)
  HasItemEquiped("OHRBELT2",Myself)  // Belt of Minor Invulnerability
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  Global("MO_CastAthkatla","LOCALS",1)
THEN
  RESPONSE #100
    UseItem("OHRBELT2",Myself)  // Belt of Minor Invulnerability
    IncrementGlobal("MO_UsedBltMnrInv","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","GLOBAL",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
THEN
  RESPONSE #100
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !Global("MO_UsedIlbratha","GLOBAL",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
  RESPONSE #100
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIlbratha","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  !Global("MO_UsedIlbratha","GLOBAL",1)
  HasItem("SW1H26",Myself)  // Ilbratha +1
  HasItemEquiped("WANINJA",Myself)  // Ninjatō of the Scarlet Brotherhood +3
THEN
  RESPONSE #100
    XEquipItem("SW1H26",Myself,SLOT_SHIELD,EQUIP)  // Ilbratha +1
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    StateCheck(Myself,STATE_MIRRORIMAGE)
    Global("MO_UsedIlbratha","GLOBAL",1)
  HasItem("WANINJA",Myself)  // Ninjatō of the Scarlet Brotherhood +3
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
  RESPONSE #100
    XEquipItem("WANINJA",Myself,SLOT_SHIELD,EQUIP)  // Ninjatō of the Scarlet Brotherhood +3
END

IF
  Global("MO_BUFF","LOCALS",1)
  GlobalLT("MO_UsedDevout","GLOBAL",1)
  HasItemEquiped("OHRHELM1",Myself)  // Headband of the Devout
THEN
  RESPONSE #100
    UseItem("OHRHELM1",Myself)  // Headband of the Devout
    IncrementGlobal("MO_UsedDevout","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
  !CheckSpellState(Myself,HARDINESS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
    RemoveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
  Global("MO_BUFF","LOCALS",1)
  HaveSpellRES("SPCL237")  // Greater Sun
  CheckStatLT(Myself,50,RESISTFIRE)
  CheckStatLT(Myself,50,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPCL237",Myself)  // Greater Sun
    RemoveSpellRES("SPCL237")  // Greater Sun
END

IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,@5004)
END