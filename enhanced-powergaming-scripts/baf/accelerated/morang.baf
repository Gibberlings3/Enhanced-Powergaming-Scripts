// **********************************************************************
// *           Segment Name: morang.baf
// **********************************************************************
// *
// * morang.baf
// * -----------
// * AI Script for:             BG2EE & BGEET
// * Written by:                Mark Evinger (morpheus562)
// * Version:                   Version 3.0 (18 June 2021)
// * Comments to:               mark.evinger@hotmail.com
// * Depends on:                BG2EE
//
//
// * Description:
// *
// * This is a script specifically designed for the Ranger class and kits. 
// * This script will cast pre-buff spells when the D key is pressed. The following 
// * spells will be pre-buffed: Armor of Faith, Divine Might, and Protection 
// * from Fire. Will pre-buff using the following potions: Potion of Heroism, 
// * Potion of Fire/Cold Resistance, Potion of Absorption, Potion of Magic 
// * Protection, and Potion of Invulnerability. Finally, the Ranger will pre-buff 
// * using mirror image from either offhand Ilbrath* or the Ring of Duplication.
//
// * In addition to pre-buffing, the script will intelligently heal and use 
// * abilities both in and out of combat. Items used by the script are: 
// * Ilbratha +1, Ring of Duplication, Amulet of the Cheetah, and Gargoyle Boots.

IF
  PartyRested()
THEN
  RESPONSE #100
    SetGlobal("MO_UsedIlbratha","GLOBAL",0)
    SetGlobal("MO_UsedDuplication","GLOBAL",0)
    SetGlobal("MO_UsedGargoyle","GLOBAL",0)
    SetGlobal("MO_UsedCheetah","GLOBAL",0)
    SetGlobal("MO_UsedFortitude","GLOBAL",0)
    SetGlobal("MO_UsedSpellTurning","GLOBAL",0)
    SetGlobal("MO_UsedClkOfStars","GLOBAL",0)
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(D)
THEN
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91061)  // Right. I'll get started then.
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(D)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn09",Myself)  // Potion of Heroism
  !CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
  RESPONSE #100
    UseItem("potn09",Myself)  // Potion of Heroism
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
  RESPONSE #100
    UseItem("potn02",Myself)  // Potion of Fire Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
  RESPONSE #100
    UseItem("potn22",Myself)  // Potion of Cold Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn18",Myself)  // Potion of Absorption
  CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
  RESPONSE #100
    UseItem("potn18",Myself)  // Potion of Absorption
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn34",Myself)  // Potion of Magic Protection
THEN
  RESPONSE #100
    UseItem("potn34",Myself)  // Potion of Magic Protection
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
  CheckStatGT(Myself,0,ARMORCLASS)
THEN
  RESPONSE #100
    UseItem("potn11",Myself)  // Potion of Invulnerability
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPRA303")
  !CheckSpellState(Myself,PROTECTION_FROM_NORMAL_MISSILES)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPRA303",Myself)
    RemoveSpellRES("SPRA303")
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
  !CheckSpellState(Myself,ARMOR_OF_FAITH)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
    RemoveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPRA302")
  CheckStatLT(Myself,1,WIZARD_SPELL_DEFLECTION)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  Global("MO_CastAthkatla","LOCALS",1)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPRA302",Myself)
    RemoveSpellRES("SPRA302")
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !CheckStatGT(Myself,0,IMPROVEDHASTE)
  GlobalLT("MO_UsedCheetah","GLOBAL",1)
  HasItemEquiped("AMUL26",Myself)  // Amulet of Cheetah Speed
THEN
  RESPONSE #100
    UseItem("AMUL26",Myself)  // Amulet of Cheetah Speed
    IncrementGlobal("MO_UsedCheetah","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
  CheckStatLT(Myself,80,RESISTFIRE)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
    RemoveSpell(CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedIlbratha","GLOBAL",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
THEN
  RESPONSE #100
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIlbratha","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","GLOBAL",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
THEN
  RESPONSE #100
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","GLOBAL",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPIN103")  // Divine Might
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  !StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
  OR(2)
    CheckStatLT(Myself,24,DEX)
    CheckStatLT(Myself,24,STR)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPIN103",Myself)  // Divine Might
    RemoveSpellRES("SPIN103")  // Divine Might
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_BLESS)  // SPPR101.SPL (Bless)
  !StateCheck(Myself,STATE_BLESS)
  !CheckSpellState(Myself,MINOR_GLOBE)
  !CheckSpellState(Myself,GLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,CLERIC_BLESS)  // SPPR101.SPL (Bless)
    RemoveSpell(CLERIC_BLESS)  // SPPR101.SPL (Bless)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
  !CheckSpellState(Myself,HARDINESS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
    RemoveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END

