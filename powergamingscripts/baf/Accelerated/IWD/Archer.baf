// **********************************************************************
// *           Segment Name: Archer.baf
// **********************************************************************
// *
// * Archer.baf
// * -----------
// * AI Script for:             BGEET with Sword Coast Stratagems
// * Written by:                Mark Evinger (morpheus562)
// * Version:                   Version 1.2 (12 February 2021)
// * Comments to:               mark.evinger@hotmail.com
// * Depends on:                BGEET
//                              Sword Coast Stratagems
//
//
// * Description:
// *
// * This is a script specifically designed for the Archer Kit. This script
// * will cast pre-buff spells when the D key is pressed. The following 
// * spells will be pre-buffed: Armor of Faith, Divine Might, and Protection 
// * from Fire. Will pre-buff using the following potions: Potion of Heroism, 
// * Potion of Fire/Cold Resistance, Potion of Absorption, Potion of Magic 
// * Protection, and Potion of Invulnerability. Finally, the Archer will pre-buff 
// * using mirror image from either offhand Ilbrath* or the Ring of Duplication.
//
// * In addition to pre-buffing, the script will intelligently heal and use 
// * abilities both in and out of combat. Items used by the script are: 
// * Ilbratha +1, Ring of Duplication, Amulet of the Cheetah, and Gargoyle Boots.

IF
  PartyRested()
THEN
  RESPONSE #100
    SetGlobal("MO_UsedIbratha","LOCALS",0)
    SetGlobal("MO_UsedDuplication","LOCALS",0)
    SetGlobal("MO_UsedGargoyle","LOCALS",0)
    SetGlobal("MO_UsedCheetah","LOCALS",0)
END

IF
  Global("MO_BUFF","LOCALS",0)
  HotKey(D)
THEN
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91061)  // Right. I'll get started then.
  RESPONSE #50
    SetGlobal("MO_BUFF","LOCALS",1)
    DisplayStringHead(Myself,91057)  // Right, I'll get started then. Please excuse me.
END

IF
  Global("MO_BUFF","LOCALS",1)
  OR(2)
    HotKey(D)
    See([EVILCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,50742)  // I have not yet completed the task.
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn09",Myself)  // Potion of Heroism
  !CheckSpellState(Myself,BUFF_ENHANCEMENT)
THEN
  RESPONSE #100
    UseItem("potn09",Myself)  // Potion of Heroism
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn02",Myself)  // Potion of Fire Resistance
THEN
  RESPONSE #100
    UseItem("potn02",Myself)  // Potion of Fire Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn22",Myself)  // Potion of Cold Resistance
THEN
  RESPONSE #100
    UseItem("potn22",Myself)  // Potion of Cold Resistance
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn18",Myself)  // Potion of Absorption
  CheckStatLT(Myself,100,RESISTELECTRICITY)
THEN
  RESPONSE #100
    UseItem("potn18",Myself)  // Potion of Absorption
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn34",Myself)  // Potion of Magic Protection
THEN
  RESPONSE #100
    UseItem("potn34",Myself)  // Potion of Magic Protection
END

IF
  Global("MO_BUFF","LOCALS",1)
  HasItemEquiped("potn11",Myself)  // Potion of Invulnerability
  CheckStatGT(Myself,0,ARMORCLASS)
THEN
  RESPONSE #100
    UseItem("potn11",Myself)  // Potion of Invulnerability
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
  !CheckSpellState(Myself,ARMOR_OF_FAITH)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
    RemoveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !CheckStatGT(Myself,0,IMPROVEDHASTE)
  GlobalLT("MO_UsedCheetah","LOCALS",1)
  HasItemEquiped("AMUL26",Myself)  // Amulet of Cheetah Speed
THEN
  RESPONSE #100
    UseItem("AMUL26",Myself)  // Amulet of Cheetah Speed
    IncrementGlobal("MO_UsedCheetah","LOCALS",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
  CheckStatLT(Myself,80,RESISTFIRE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
    RemoveSpell(CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPIN103")  // Divine Might
  !StateCheck(Myself,STATE_DRAWUPONHOLYMIGHT)
  CheckStatLT(Myself,24,DEX)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    ReallyForceSpellRES("SPIN103",Myself)  // Divine Might
    RemoveSpellRES("SPIN103")  // Divine Might
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedIbratha","LOCALS",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
THEN
  RESPONSE #100
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIbratha","LOCALS",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","LOCALS",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
THEN
  RESPONSE #100
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","LOCALS",1)
END

IF
  Global("MO_BUFF","LOCALS",1)
  IfValidForPartyDialog(Myself)
  HaveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
  !CheckSpellState(Myself,HARDINESS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
    RemoveSpell(WARRIOR_HARDINESS)  // SPCL907.SPL (Hardiness)
END

IF
  Global("MO_BUFF","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("MO_BUFF","LOCALS",0)
    DisplayStringHead(Myself,421)  // Combat ready!
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  OR(2)
    StateCheck(Myself,STATE_POISONED)
    CheckSpellState(Myself,DISEASED)
  HasItemEquiped("POTN17",Myself)  // Elixir of Health
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN17",Myself)  // Elixir of Health
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(Myself,STATE_POISONED)
  HasItemEquiped("POTN20",Myself)  // Antidote
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN20",Myself)  // Antidote
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HasItemEquiped("POTN55",Myself)  // Potion of Superior Healing
  ActuallyInCombat()
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN55",Myself)  // Potion of Superior Healing
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HasItemEquiped("POTN52",Myself)  // Potion of Extra Healing
  ActuallyInCombat()
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN52",Myself)  // Potion of Extra Healing
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HasItemEquiped("POTN17",Myself)  // Elixir of Health
  ActuallyInCombat()
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN17",Myself)  // Elixir of Health
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([EVILCUTOFF])
  HPPercentLT(Myself,51)
  HasItemEquiped("POTN08",Myself)  // Potion of Healing
  ActuallyInCombat()
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("POTN08",Myself)  // Potion of Healing
    DisplayString(Myself,46150)  // *quaffs a potion*
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See([PC])
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See(SecondNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See(ThirdNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See(FourthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See(FifthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpellRES("SPIN102")  // Cure Affliction
  See(SixthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  OR(2)
    Range(LastSeenBy(Myself),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    SpellRES("SPIN102",LastSeenBy(Myself))  // Cure Affliction
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  HaveSpellRES("SPIN101")  // Healing Touch
  !See([EVILCUTOFF])
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),80)
  IfValidForPartyDialog(LastSeenBy(Myself))
  OR(2)
    Range(MostDamagedOf([PC]),6)
    !Range(NearestEnemyOf(Myself),30)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SpellRES("SPIN101",LastSeenBy(Myself))  // Healing Touch
    SetInterrupt(TRUE)
END

IF
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","LOCALS",2)
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    UseItem("Boot12",Myself)  // Gargoyle Boots
    IncrementGlobal("MO_UsedGargoyle","LOCALS",1)
END

IF
  CheckStatLT(Myself,1,STONESKINS)
  GlobalLT("MO_UsedGargoyle","LOCALS",2)
  HasItem("Boot12",Myself)  // Gargoyle Boots
  HasItemEquiped("Boot01",Myself)  // The Paws of the Cheetah
THEN
  RESPONSE #100
    XEquipItem("Boot12",Myself,SLOT_BOOTS,EQUIP)  // Gargoyle Boots
END

IF
  OR(2)
    CheckStatGT(Myself,0,STONESKINS)
    GlobalGT("MO_UsedGargoyle","LOCALS",1)
  HasItem("Boot01",Myself)  // The Paws of the Cheetah
  HasItemEquiped("Boot12",Myself)  // Gargoyle Boots
THEN
  RESPONSE #100
    XEquipItem("Boot01",Myself,SLOT_BOOTS,EQUIP)  // The Paws of the Cheetah
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedIbratha","LOCALS",1)
  HasItemEquiped("SW1H26",Myself)  // Ilbratha +1
  See(NearestEnemyOf(Myself))
  OR(11)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    NumCreatureGT([EVILCUTOFF],3)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItemAbility("SW1H26",Myself,SLOT_AMMO3,1)  // Ilbratha +1
    IncrementGlobal("MO_UsedIbratha","LOCALS",1)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("MO_UsedDuplication","LOCALS",3)
  HasItemEquiped("OHRRING1",Myself)  // Ring of Duplication
  See(NearestEnemyOf(Myself))
  OR(11)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    NumCreatureGT([EVILCUTOFF],3)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("OHRRING1",Myself)  // Ring of Duplication
    IncrementGlobal("MO_UsedDuplication","LOCALS",1)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(Myself)
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See([PC])
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(SecondNearest([PC]))
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(ThirdNearest([PC]))
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(FourthNearest([PC]))
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(FifthNearest([PC]))
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  See(SixthNearest([PC]))
  OR(5)
    StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
    StateCheck(LastSeenBy(Myself),STATE_BLIND)
    StateCheck(LastSeenBy(Myself),STATE_DISEASED)
    CheckSpellState(LastSeenBy(Myself),DISEASED)
    CheckSpellState(LastSeenBy(Myself),DEAFENED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_CURE_DISEASE)  // SPPR317.SPL (Cure Disease)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(Myself)
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(Myself,CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See([PC])
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(SecondNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(ThirdNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(FourthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(FifthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
  See(SixthNearest([PC]))
  StateCheck(LastSeenBy(Myself),STATE_POISONED)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_SLOW_POISON)  // SPPR212.SPL (Slow Poison)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck([GOODCUTOFF],STATE_PANIC)
  See([GOODCUTOFF])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(SecondNearest([GOODCUTOFF]),STATE_PANIC)
  See(SecondNearest([GOODCUTOFF]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(ThirdNearest([GOODCUTOFF]),STATE_PANIC)
  See(ThirdNearest([GOODCUTOFF]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(FourthNearest([GOODCUTOFF]),STATE_PANIC)
  See(FourthNearest([GOODCUTOFF]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(FifthNearest([GOODCUTOFF]),STATE_PANIC)
  See(FifthNearest([GOODCUTOFF]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  StateCheck(SixthNearest([GOODCUTOFF]),STATE_PANIC)
  See(SixthNearest([GOODCUTOFF]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  HaveSpell(CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    SetInterrupt(FALSE)
    Spell(LastSeenBy(Myself),CLERIC_REMOVE_FEAR)  // SPPR108.SPL (Remove Fear)
    SetInterrupt(TRUE)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !See([EVILCUTOFF])
  HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)  // SPPR315.SPL (Cure Medium Wounds)
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),70)
  IfValidForPartyDialog(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_CURE_MEDIUM_WOUNDS)  // SPPR315.SPL (Cure Medium Wounds)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !See([EVILCUTOFF])
  HaveSpell(CLERIC_CURE_MODERATE_WOUNDS)  // sppr215.SPL (Cure Moderate Wounds)
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),80)
  IfValidForPartyDialog(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_CURE_MODERATE_WOUNDS)  // sppr215.SPL (Cure Moderate Wounds)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !See([EVILCUTOFF])
  HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)  // SPPR103.SPL (Cure Light Wounds)
  See(MostDamagedOf([PC]))
  HPPercentLT(LastSeenBy(Myself),90)
  IfValidForPartyDialog(LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
  CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
  !CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
  !CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
  !CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
  !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)
  CheckStatLT(Myself,50,SPELLFAILUREPRIEST)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),CLERIC_CURE_LIGHT_WOUNDS)  // SPPR103.SPL (Cure Light Wounds)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
  See([EVILCUTOFF])
  NumCreatureGT([EVILCUTOFF],3)
  !CheckSpellState(Myself,ARMOR_OF_FAITH)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_ARMOR_OF_FAITH)  // SPPR111.SPL (Armor of Faith)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
  OR(7)
    Class(NearestEnemyOfType,MAGE_ALL)
    Class(NearestEnemyOfType,SORCERER)
    Class(NearestEnemyOfType,DRUID_ALL)
    Class(NearestEnemyOfType,CLERIC_ALL)
    Class(NearestEnemyOfType,ELEMENTAL_FIRE)
    Class(NearestEnemyOfType,RED_DRAGON)
    Class(NearestEnemyOfType,SILVER_DRAGON)
  CheckStatLT(Myself,80,RESISTFIRE)
  CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    Spell(Myself,CLERIC_PROTECTION_FROM_FIRE)  // SPPR306.SPL (Protection From Fire)
END

IF
  ActionListEmpty()
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(WARRIOR_GREATER_WHIRLWIND)  // SPCL901.SPL (Greater Whirlwind Attack)
  !CheckStat(Myself,5,OFFENSIVE_MODIFIER)
  !CheckSpellState(Myself,ARCHER_CALL_SHOT)
  See([EVILCUTOFF])
  OR(6)
    WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(6)
    WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(34)
    NumCreatureGT([EVILCUTOFF],3)
    Range([EVILCUTOFF.0.GOLEM],30)
    Range([EVILCUTOFF.0.MIST],30)
    Range([EVILCUTOFF.0.KUO-TOA],30)
    Range([EVILCUTOFF.0.GENIE],30)
    Range([EVILCUTOFF.0.GITHYANKI],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.VAMPYRE],30)
    Range([EVILCUTOFF.0.MIND_FLAYER],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.BEHOLDER],30)
    Range([EVILCUTOFF.0.UMBERHULK],30)
    Range([EVILCUTOFF.0.MYCONID],30)
    Range([EVILCUTOFF.0.VAMPIRE],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.0.WRAITH],30)
    Range([EVILCUTOFF.0.0.WIGHT],30)
    Range([EVILCUTOFF.0.0.SPECTRE],30)
    Range([EVILCUTOFF.0.0.WYVERN],30)
    Range([EVILCUTOFF.0.0.BASILISK],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    See([ENEMY.0.0.MAGE_ALL])
    See([ENEMY.0.0.BARD_ALL])
    See([ENEMY.0.0.CLERIC_ALL])
    See([ENEMY.0.0.DRUID_ALL])
    See([ENEMY.0.0.SORCERER])
    See([ENEMY.0.0.CLERIC_MAGE])
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    Spell(Myself,WARRIOR_GREATER_WHIRLWIND)  // SPCL901.SPL (Greater Whirlwind Attack)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  GlobalLT("MO_UsedCheetah","LOCALS",1)
  HasItemEquiped("AMUL26",Myself)  // Amulet of Cheetah Speed
  !CheckStatGT(Myself,0,IMPROVEDHASTE)
  !CheckStat(Myself,5,OFFENSIVE_MODIFIER)
  See([EVILCUTOFF])
  OR(6)
    WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(6)
    WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(34)
    NumCreatureGT([EVILCUTOFF],3)
    Range([EVILCUTOFF.0.GOLEM],30)
    Range([EVILCUTOFF.0.MIST],30)
    Range([EVILCUTOFF.0.KUO-TOA],30)
    Range([EVILCUTOFF.0.GENIE],30)
    Range([EVILCUTOFF.0.GITHYANKI],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.VAMPYRE],30)
    Range([EVILCUTOFF.0.MIND_FLAYER],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.BEHOLDER],30)
    Range([EVILCUTOFF.0.UMBERHULK],30)
    Range([EVILCUTOFF.0.MYCONID],30)
    Range([EVILCUTOFF.0.VAMPIRE],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.0.WRAITH],30)
    Range([EVILCUTOFF.0.0.WIGHT],30)
    Range([EVILCUTOFF.0.0.SPECTRE],30)
    Range([EVILCUTOFF.0.0.WYVERN],30)
    Range([EVILCUTOFF.0.0.BASILISK],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    See([ENEMY.0.0.MAGE_ALL])
    See([ENEMY.0.0.BARD_ALL])
    See([ENEMY.0.0.CLERIC_ALL])
    See([ENEMY.0.0.DRUID_ALL])
    See([ENEMY.0.0.SORCERER])
    See([ENEMY.0.0.CLERIC_MAGE])
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_SILENCED)
  !CheckSpellState(Myself,WILD_MAGIC_AREA)
  !CheckSpellState(Myself,DEAD_MAGIC_AREA)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    UseItem("AMUL26",Myself)  // Amulet of Cheetah Speed
    IncrementGlobal("MO_UsedCheetah","LOCALS",1)
END

IF
  ActionListEmpty()
  IfValidForPartyDialog(Myself)
  !GlobalTimerNotExpired("MO_SpellCast","LOCALS")
  HaveSpell(ARCHER_CALL_SHOT)  // SPCL121.SPL (Called Shot)
  !CheckStat(Myself,5,OFFENSIVE_MODIFIER)
  !CheckSpellState(Myself,ARCHER_CALL_SHOT)
  See([EVILCUTOFF])
  OR(6)
    WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(6)
    WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
    WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
  OR(34)
    NumCreatureGT([EVILCUTOFF],3)
    Range([EVILCUTOFF.0.GOLEM],30)
    Range([EVILCUTOFF.0.MIST],30)
    Range([EVILCUTOFF.0.KUO-TOA],30)
    Range([EVILCUTOFF.0.GENIE],30)
    Range([EVILCUTOFF.0.GITHYANKI],30)
    Range([EVILCUTOFF.0.RAKSHASA],30)
    Range([EVILCUTOFF.0.VAMPYRE],30)
    Range([EVILCUTOFF.0.MIND_FLAYER],30)
    Range([EVILCUTOFF.0.DRAGON],30)
    Range([EVILCUTOFF.0.DEMONIC],30)
    Range([EVILCUTOFF.0.BEHOLDER],30)
    Range([EVILCUTOFF.0.UMBERHULK],30)
    Range([EVILCUTOFF.0.MYCONID],30)
    Range([EVILCUTOFF.0.VAMPIRE],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.DEMILICH],30)
    Range([EVILCUTOFF.0.FAIRY],30)
    Range([EVILCUTOFF.0.0.WRAITH],30)
    Range([EVILCUTOFF.0.0.WIGHT],30)
    Range([EVILCUTOFF.0.0.SPECTRE],30)
    Range([EVILCUTOFF.0.0.WYVERN],30)
    Range([EVILCUTOFF.0.0.BASILISK],30)
    Range([EVILCUTOFF.0.LICH],30)
    Range([EVILCUTOFF.0.SOLAR],30)
    Range([EVILCUTOFF.0.PLANATAR],30)
    Range([EVILCUTOFF.0.ANTISOLAR],30)
    Range([EVILCUTOFF.0.DARKPLANATAR],30)
    See([ENEMY.0.0.MAGE_ALL])
    See([ENEMY.0.0.BARD_ALL])
    See([ENEMY.0.0.CLERIC_ALL])
    See([ENEMY.0.0.DRUID_ALL])
    See([ENEMY.0.0.SORCERER])
    See([ENEMY.0.0.CLERIC_MAGE])
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCast","LOCALS",ONE_ROUND)
    Spell(Myself,ARCHER_CALL_SHOT)  // SPCL121.SPL (Called Shot)
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    Attack(SixthNearestEnemyOf(Myself))
END

